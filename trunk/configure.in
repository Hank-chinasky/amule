# Process this file with autoconf to produce a configure script.
AC_INIT([aMule],[CVS],[deltahf@datahighways.de])
AM_INIT_AUTOMAKE
AC_CONFIG_SRCDIR([src/SharedFileList.h])
AM_CONFIG_HEADER([config.h])
AMULE_VERSION="CVS"
AM_MAINTAINER_MODE

# No -O2 by default
CFLAGS=" $CFLAGS"
CXXFLAGS=" $CXXFLAGS"

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
# Checks for libraries.

AM_GNU_GETTEXT
AM_GNU_GETTEXT_VERSION(0.11.5)

AM_OPTIONS_WXCONFIG

AM_PATH_WXCONFIG(2.4.0, WXFOUND=1)

if test "$WXFOUND" != 1; then
    AC_MSG_ERROR([
        Please check that wx-config is in path, the directory
        where wxWidgets libraries are installed (returned by
        'wx-config --libs' command) is in LD_LIBRARY_PATH or
        equivalent variable and wxWidgets is version 2.4.0 or above.
	Or this might also be a bug in our configure. Please try again
	with --with-wx-config=/usr/bin/wx-config
	(replace /usr/bin/wx-config with a valid path to your wx-config)
	* Note:
	Most probably, either one of the above aren't correct, you don't
	have wxGTK installed, or are missing wxGTK-devel (or equivalent) package.
    ])
fi

AM_OPTIONS_WXBASECONFIG

AM_PATH_WXBASECONFIG(2.4.0, WXBASEFOUND=1)

if test "$WXBASEFOUND" != 1; then
    AC_MSG_NOTICE([
	WARNING: libwx_base >= 2.4.0 is not found.
	This is not needed for compiling aMule, but it is for compiling amulecmd.
	If you have no wxbase, amulecmd will be linked against wxgtk, thus removing
	all the non-graphical client meaning at all.
        Please check that wxbase-2.4-config is in path, the directory
        where wxWidgets base libraries are installed (returned by
        'wxbase-2.4-config --libs' command) is in LD_LIBRARY_PATH or
        equivalent variable and wxWidgets base is version 2.4.0 or above.
	Or this might also be that your wxbase-config has other name.
	Please try again with --with-wxbase-config=/usr/bin/wxbase-config
	(replace /usr/bin/wxbase-config with a valid path to your wxbase-config)
	* Note:
	Most probably, either one of the above aren't correct, you don't
	have wxbase installed, or are missing wxbase-devel (or equivalent) package.
    ])
fi

AM_OPTIONS_CURLCONFIG

AM_PATH_CURLCONFIG(7.9.7,CURLFOUND=1)

if test "$CURLFOUND" != 1; then
    AC_MSG_ERROR([
	WARNING: curl >= 7.9.7 is not found.
	Please check that curl-config is in your default path, check out
        LD_LIBRARY_PATH or equivalent variable.
	Or this might also be that your curl-config has other name.
	Please try again with --with-curl-config=/usr/bin/curl-config
	(replace /usr/bin/curl-config with a valid path to your curl-config).
	If you use compiled packages check if you have devel pack installed.
	To download the lastest version check http://curl.haxx.se/download.html for sources.
    ])
fi


CHECK_WX_BUILT_WITH_GTK2

if test x"$GTK_USEDVERSION" = x"2" ; then
  AM_PATH_GTK_2_0(2.0.3, havegtk2=yes, havegtk2=no)
  GTK_DEFS="-D__GTK2__"
  GET_GTK2_VERSION
else
  AM_PATH_GTK(1.2.0, havegtk=yes, havegtk=no)
  GTK_DEFS=""
  GET_GTK_VERSION
fi

CHECK_WX_PARTIAL_VERSION

if test x"$WX_PARTIAL_VERSION" = x"5"; then
  WX_VERSION="2_5"
else
  WX_VERSION="any"
fi

AM_OPTIONS_CRYPTO


CHECK_CRYPTO(5.1,CRYPTO=0)

if test "$CRYPTO" != 0; then
    AC_MSG_ERROR([
	WARNING: crypto++ >= 5.1 is not found.
	Please check that cryptopp-headers are in your default path, check out
        LD_LIBRARY_PATH or equivalent variable.
	Or this might also be that your cryptopp is instaled on other path.
	Please try again with --with-crypto-prefix=/usr/include/
	(replace  /usr/include/ with a valid path to your crypto directory).
	The crypto folder must me named cryptopp. Sorry about the mess but it's the
	only way because libcrypto has no make install.
	If you use compiled packages use --with-crypto-lib to specify the lib path.
	To download the lastest version check http://www.cryptopp.com for sources.
    ])
fi

CHECK_ZLIB

GET_WXGTK_VERSION

# Add args to configure
AC_ARG_ENABLE(debug,   [  --enable-debug          Enable additional debugging output],
              USE_DEBUG="$enableval", USE_DEBUG="no")

AC_ARG_ENABLE(debug,   [  --disable-debug-syms    Disable debugging information],
              USE_DEBUG_SYMS="$disableval", USE_DEBUG_SYMS="yes")

AC_ARG_ENABLE(optimise,[  --enable-optimise       Enable code optimising],
		USE_OPTIMISE="$enableval", USE_OPTIMISE="no")

AC_ARG_ENABLE(profile, [  --enable-profile        Enable code profiling],
		USE_PROFILE="$enableval", USE_PROFILE="no")

AC_ARG_ENABLE(static,  [  --enable-static         Produce a statically linked executable],
              USE_DEBUG_STATIC="$enableval", USE_DEBUG_STATIC="no")

AC_ARG_ENABLE(gsocket, [  --disable-gsocket       Disable gsocket.c linking],
		USE_GSOCKET="$disableval", USE_GSOCKET="yes")

# systray disable
AC_MSG_CHECKING([whether SysTray should be compiled])
AC_ARG_ENABLE(systray, [  --disable-systray       Disable SysTray compilation],
			systray=$enableval, systray=yes)
AC_MSG_RESULT($systray)
AC_SUBST(systray)

# safe systray
AC_MSG_CHECKING([whether SysTray should be compiled in safe mode])
AC_ARG_ENABLE(safetray,[  --enable-safetray       Enable SysTray in safe mode only],
               USE_SAFE_TRAY="$enableval", USE_SAFE_TRAY="no")
AC_MSG_RESULT($USE_SAFE_TRAY)
AC_SUBST(USE_SAFE_TRAY)

# gtk code strip
AC_MSG_CHECKING([whether pure GTK code should be compiled])
AC_ARG_ENABLE(gtk, [  --disable-gtk           Disable pure GTK code in amule], gtk="$enableval", gtk="yes")
AC_MSG_RESULT($gtk)
AC_SUBST(gtk)

# debugging only
AC_MSG_CHECKING([whether progress bar drawing should be disabled])
AC_ARG_ENABLE(bardraw, [  --disable-bardraw         Disable progress bar drawing],
		DISABLE_PROGRESS="$enableval", DISABLE_PROGRESS="no")
AC_MSG_RESULT($DISABLE_PROGRESS)
AC_SUBST(DISABLE_PROGRESS)

AC_MSG_CHECKING([whether progress amulecmd should be build])
AC_ARG_ENABLE(amulecmd, [  --disable-amulecmd      Disable amulecmd build],
		BUILDAMULECMD="$enableval", BUILDAMULECMD="yes")
AC_MSG_RESULT($BUILDAMULECMD)
AC_SUBST(BUILDAMULECMD)


# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([arpa/inet.h fcntl.h locale.h netdb.h netinet/in.h sys/socket.h sys/time.h sys/timeb.h sys/resource.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

# Checks for library functions.
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_MKTIME
AC_TYPE_SIGNAL
AC_FUNC_STAT
AC_FUNC_STRFTIME
AC_CHECK_FUNCS([floor ftruncate gettimeofday inet_ntoa memchr memmove memset mkdir putenv select setlocale sqrt strcasecmp strchr strdup strrchr strstr strtoul getrlimit setrlimit])

# Other tests

# Other tests
case "${host}" in
powerpc-apple-darwin*) LDFLAGS="$LDFLAGS -bind_at_load"
	;;
esac


case "$USE_DEBUG" in
yes)	DEBUG_FLAGS="-Wall -g -ggdb -fno-inline -D__DEBUG__"
	USE_DEBUG_SYMS="yes"
    ;;
*)	DEBUG_FLAGS=""
    ;;
esac

case "$WX_VERSION" in
2_5)	USE_24GSOCKET="no"
    ;;
*)	USE_24GSOCKET="yes"
    ;;
esac

case "$USE_RELEASE" in
yes)	CXXFLAGS="-DRELEASE_GROUP_MODE $CXXFLAGS"
    ;;
esac

case "$USE_DEBUG_SYMS" in
yes)	DEBUG_FLAGS="$DEBUG_FLAGS -g"
	;;
esac

case "$BUILDAMULECMD" in
yes)	BUILD_AMULECMD="yes"
	;;
*)	BUILD_AMULECMD="no"
	;;
esac

case "$WXBASEFOUND" in
1)	HAVE_AMULECMD="yes"
	WXBASE_CXXFLAGS="$WXBASE_CXXFLAGS -DWXBASE"
	;;
*)	HAVE_AMULECMD="no"
	;;
esac

case "$USE_OPTIMISE" in

yes)	OPTIMISE_FLAGS="-O2"
	;;
*)	OPTIMISE_FLAGS=""
	;;
esac

case "$USE_SAFE_TRAY" in
yes) TRAY_FLAGS="-D__SAFE_TRAY__"
    ;;
*) TRAY_FLAGS=""
    ;;
esac

case "$USE_PROFILE" in
yes)	PROFILE_FLAGS="-pg"
	;;
*)	PROFILE_FLAGS=""
	;;
esac

case "$USE_DEBUG_STATIC" in
yes)	LDFLAGS="$LDFLAGS -static"
	WX_LIBS="$WX_LIBS `gtk-config --libs` -lgthread"
	;;
esac

case "$systray" in
no) TRAY_FLAGS="-D__SYSTRAY_DISABLED__"
     USE_SAFE_TRAY="no"
     ;;
esac

case "$DISABLE_PROGRESS" in
yes)     AC_DEFINE_UNQUOTED(DISABLE_PROGRESS, 1,
      [Define if progress bar drawing should be disabled.])
  ;;
esac

case "$gtk" in
no) GTK_DEFS="$GTK_DEFS -D__NOGTK__"
	;;
esac

if test "$CURLFOUND" != "no" ; then
  LIBS="$LIBS $CURL_LIBS"
  CXXFLAGS="$CXXFLAGS $CURL_FLAGS"
fi

case "$CRYPTO_PP_STYLE" in
gentoo_debian) CXXFLAGS="$CXXFLAGS -I$crypto_prefix -D__CRYPTO_DEBIAN_GENTOO__"
# Debian uses libcrypto++5.1 - it's not my fault, please soda patch the package
	LIBS="$LIBS -lcrypto++"
	;;
mdk_suse_fc) CXXFLAGS="$CXXFLAGS -I$crypto_prefix -D__CRYPTO_MDK_SUSE_FC__"
	LIBS="$LIBS -lcryptopp"
	;;
sources) CXXFLAGS="$CXXFLAGS -I$crypto_prefix -D__CRYPTO_SOURCE__"
	LIBS="$LIBS -lcryptopp"
	;;
esac

AC_DEFINE_UNQUOTED(LOCALEDIR, "`eval echo $datadir/locale`", [Define where the locales are.])
# we have it anyway by now

XRCFLAGS="-Isrc"
CXXFLAGS="$CXXFLAGS $DEBUG_FLAGS $OPTIMISE_FLAGS $PROFILE_FLAGS"
CFLAGS="$CFLAGS $DEBUG_FLAGS $OPTIMISE_FLAGS $PROFILE_FLAGS"
LDFLAGS="$LDFLAGS $PROFILE_FLAGS"
AM_CONDITIONAL(COMPILE_CMD, test x$BUILD_AMULECMD = xyes)
AM_CONDITIONAL(HAVE_WXBASE, test x$HAVE_AMULECMD = xyes)
AM_CONDITIONAL(ADD_24GSOCKET, test x$USE_24GSOCKET = xyes)
AM_CONDITIONAL(ADD_GSOCKET, test x$USE_GSOCKET = xyes)
AC_SUBST(GTK_DEFS)
AC_SUBST(TRAY_FLAGS)


# DO NOT MOVE UP...THAT'S A REASON TO BE HERE!!

AM_OPTIONS_CCACHE_PFX

AC_MSG_CHECKING([whether ccache support should be added])
AC_ARG_ENABLE(ccache, [  --enable-ccache   enable ccache support for fast recompilation],
		ccache="$enableval", ccache="no")
AC_MSG_RESULT($ccache)
AC_SUBST(ccache)

case "$ccache" in

yes)
	CHECK_CCACHE
	if [[ $CCACHE = 0 ]]; then
	GCC="$ccache_prefix/ccache $GCC"
	CC="$ccache_prefix/ccache $CC"
	GXX="$ccache_prefix/ccache $GXX"
	CXX="$ccache_prefix/ccache $CXX"
	else
	GCC="$GCC"
	CC="$CC"
	GXX="$GXX"
	CXX="$CXX"
	fi
	;;
*)      GCC="$GCC"
	CC="$CC"
	GXX="$GXX"
	CXX="$CXX"
esac

AC_CONFIG_FILES([src/Makefile intl/Makefile po/Makefile.in m4/Makefile Makefile Compilation.flags])
AC_OUTPUT

echo
echo
echo "  Configure script has finished system check."
echo
echo "  Configured aMule ${AMULE_VERSION} for '${host}'."
echo
echo "  Should aMule be compiled in debug mode?                  ${USE_DEBUG:-no}"
echo "  Should aMule be compiled with release grp code?          ${USE_RELEASE:-no}"
echo "  Should aMule be compiled without debug reporting?        ${USE_DEBUG_SYMS:-no}"
echo "  Should aMule be compiled with profiling?                 ${USE_PROFILE:-no}"
echo "  Should aMule be compiled with optimizations?             ${USE_OPTIMISE:-no}"
echo "  Should aMule be compiled with systray?                   ${systray}"
echo "  Should aMule be compiled with safe systray?              ${USE_SAFE_TRAY:-no}"
echo "  Should aMule be compiled with pure GTK code?             ${gtk}"
echo "  Should aMule be compiled without bar drawing?            ${DISABLE_PROGRESS:-no}"
echo "  Should aMule be linked against patched gsocket?          ${USE_24GSOCKET:-no}"
case "$BUILDAMULECMD" in
yes)	echo "  Should amulecmd (TextClient) be linked against wxbase?   ${HAVE_AMULECMD:-no}"
	;;
esac
echo "  Should ccache support be enabled?                        ${ccache:-no}"
echo "  Should amulecmd be built?                                ${BUILD_AMULECMD:-no}"
echo "  Crypto++ library/headers style?                          ${CRYPTO_PP_STYLE}"
echo
echo "  Which libraries should aMule use?"
echo "                                       wxWidgets          ${WXGTK_VERSION:-Not detected}"
echo "                                       GTK                ${GTK_VERSION:-Not detected}"
echo ""
